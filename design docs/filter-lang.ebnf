Root ::= RootExpr+

RootExpr ::= Module | 
    Rib | 
    "view" Identifier 'contains' TypeIdentifier '{' RibBody '}' | 
    "prefix-list" Identifier '{' PrefixListbody '}' |
    "table" Identifier '{' TableBody '}' | 
    Comment

Module ::= "module" Identifier 'for' Identifier WithStatement '{' ModuleBody '}'
ModuleBody ::= Define ModuleExpr+ Apply

ModuleExpr ::= Define |
   Term+ | 
   Action+ |
   'import' ForStatement '{' ImportBody '}'

Define ::= 'define' ForStatement  WithStatement '{' DefineBody '}'
DefineBody ::= ( ( 'use' Identifier ';' )? ( Identifier '=' CallExpr ';' )+ )+

Term ::= 'term' Identifier '{' TermBody '}' ForStatement WithStatement '{' DefineBody '}'
TermBody ::= (
      ('use' Identifier ';')?
      ( MatchOperator '{' (CallExpr ';')+ '}' )+
)+

Action ::= 'action' Identifier '{' ActionBody '}' ForStatement WithStatement '{' ActionBody '}'
ActionBody ::= ( CallExpr ';' )+

Apply ::= 'apply' ForStatement WithStatement '{' ApplyBody '}'
ApplyBody ::= ApplyScope+ ( AcceptReject ';' )?
ApplyScope ::= 
     ( 'use' Identifier ';' )?
     'filter' MatchOperator ArgExpr
     'not'? 'matching'
     '{' ( ArgExpr ';' ( AcceptReject ';' )? )+ '}' ';'

Rib ::= "rib" Identifier 'contains' TypeIdentifier '{' RibBody '}'
RibBody ::= ( Identifier ':' ( TypeIdentifier | ( '{' RibBody '}' ) ) ','? )+

ForStatement ::= ( 'for' Identifier ':' TypeIdentifier )?
WithStatement ::= ( 'with' ( Identifier ':' TypeIdentifier )+ )*

FieldExpr ::= Identifier ( '.' Identifier )+
ArgExprList ::= ( ArgExpr ','? )*
CallReceiver ::= Identifier | FieldExpr
CallExpr ::= CallReceiver '(' ArgExprList ')' ';'

ArgExpr ::= Identifier | TypeIdentifier | StringLiteral | Bool | CallExpr | FieldExpr | PrefixMatchExpr

MatchOperator ::= 'exactly-one' | 'some' | 'all'

MatchExpr ::= ArgExpr | GroupedExpr | CompareExpr | AndExpr | OrExpr | SetCompareExpr
CompareExpr ::= ArgExpr ( '==' | '!=' | '<' | '<=' | '>' | '>=' ) ArgExpr

PrefixMatchExpr ::= Prefix PrefixMatchType

PrefixMatchType ::= ( 'exact' | 'longer' | 'orlonger' | 
    'prefix-length-range' | 'upto' | 'through' | 'netmask' ) 
    ( PrefixLength | PrefixLengthRange | IpAddress )

IpAddress ::= IpV4Address | IpV6Address
Prefix ::= IpAddress PrefixLength
PrefixLength ::= '/' [0-9]+
PrefixLengthRange ::= PrefixLength '-' PrefixLength

AcceptReject ::= 'return'? ( 'accept' | 'reject' )

Identifier ::= ([a-z]) ([0-9a-z_-])*
TypeIdentifier ::= [A-Z] ([0-9a-zA-Z])*

Comment ::= '//' ( [^\n] )* '\n'

StringLiteral ::= '"' Identifier '"'
Bool ::= 'true' | 'false'
